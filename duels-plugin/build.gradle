import org.apache.tools.ant.filters.ReplaceTokens

clean.doFirst {
    delete "$rootDir/out/"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [VERSION: project.version])
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.0.1'
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    compileOnly 'io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'com.SirBlobman.combatlogx:CombatLogX-API:10.0.0.0-SNAPSHOT'
    compileOnly ('net.essentialsx:EssentialsX:2.19.2') {
        transitive = false
    }

    compileOnly fileTree(dir: new File(rootDir, "libs"), include: ['*.jar'])

    implementation project(':duels-api')
    implementation project(':duels-worldguard')
    implementation project(':duels-worldguard-v6')
    implementation project(':duels-worldguard-v7')
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
}

jar {
    enabled = false
}

shadowJar {
    getArchiveFileName().set(parent.name + '-' + project.version + '.jar')

    final String group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
    relocate 'com.fasterxml.jackson.core', group + 'jackson-core'
}

// To build Duels plugin jar, run './gradlew clean build'.
assemble {
    dependsOn(shadowJar)
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group.toString()
            artifactId = project.name.toLowerCase()
            version = project.version
            from components.java
        }
    }
}
